@echo off
goto #_undefined_# 2>NUL || title %COMSPEC% & call "%~dp0\activate-juliawin-environment.bat"

:: Activate Conda if installed
if exist "%juliawin_packages%\conda\Scripts\activate.bat" (
    goto #_undefined_# 2>NUL || title %COMSPEC% & call "%juliawin_packages%\conda\Scripts\activate.bat"
)

:: If not parameters provided, open prompts, otherwise `run` the arguments directly
if "%~1%~2%~3%~4%~5%~6%~7%~8%~9" equ "" ( 
    if "%juliawin_sh%" neq "" (
        goto #_undefined_# 2>NUL || title %COMSPEC% & call "%juliawin_sh%"
    ) else (
        goto #_undefined_# 2>NUL || title %COMSPEC% & call cmd
    )
) else (

    REM note that without "call", any exit will quit the whole chain
    goto #_undefined_# 2>NUL || title %COMSPEC% & call "%~dp0\execute-with-juliawin-sh.bat" %*
)
